plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.thijs226'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven { 
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/repositories/snapshots' 
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
    implementation 'org.xerial:sqlite-jdbc:3.46.0.0'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set('FAHResourceDonor')
    archiveClassifier.set('')
    archiveVersion.set(version)
    
    // Relocate SQLite to avoid conflicts
    relocate 'org.sqlite', 'com.thijs226.fahdonor.lib.sqlite'
}

build {
    dependsOn shadowJar
}